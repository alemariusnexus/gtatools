# Copyright 2010-2012 David "Alemarius Nexus" Lerch
# 
# This file is part of gtatools-gui.
#
# gtatools-gui is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
# 
# gtatools-gui is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with gtatools-gui.  If not, see <http://www.gnu.org/licenses/>.

# Additional permissions are granted, which are listed in the file
# GPLADDITIONS.

CMAKE_MINIMUM_REQUIRED(VERSION 2.6)
PROJECT(prj-gtatools-gui C CXX)

INCLUDE(CheckSymbolExists)

ADD_DEFINITIONS(-D__STDC_LIMIT_MACROS)

ADD_SUBDIRECTORY(src)

IF(prj-gtaformats_SOURCE_DIR)
    SET(GTAFORMATS_LIB_PATH gtaformats)
    SET(GTAFORMATS_INCLUDE_PATH "${prj-gtaformats_SOURCE_DIR}" "${prj-gtaformats_BINARY_DIR}")
ELSE(prj-gtaformats_SOURCE_DIR)
    FIND_LIBRARY(GTAFORMATS_LIB_PATH NAMES gtaformats libgtaformats)
    FIND_PATH(GTAFORMATS_INCLUDE_PATH gtaformats/config.h)
ENDIF(prj-gtaformats_SOURCE_DIR)

IF(prj-gta_SOURCE_DIR)
    SET(GTA_LIB_PATH gta)
    SET(GTA_INCLUDE_PATH "${prj-gta_SOURCE_DIR}" "${prj-gta_BINARY_DIR}")
ELSE(prj-gta_SOURCE_DIR)
    FIND_LIBRARY(GTA_LIB_PATH NAMES gta libgta)
    FIND_PATH(GTA_INCLUDE_PATH gta/config.h)
ENDIF(prj-gta_SOURCE_DIR)

FIND_PATH(SQUISH_INCLUDE_PATH squish.h)
FIND_LIBRARY(SQUISH_LIB_PATH NAMES squish libsquish)

FIND_PATH(PVRTEXLIB_INCLUDE_PATH PVRTexLib.h)
FIND_LIBRARY(PVRTEXLIB_LIB_PATH NAMES PVRTexLib libPVRTexLib)

FIND_PACKAGE(Boost)
FIND_PACKAGE(Bullet COMPONENTS BulletCollision BulletDynamics LinearMath)

IF(GTA_USE_OPENGL_ES)
    FIND_LIBRARY(GLESV2_LIB_PATH NAMES GLESv2 libGLESv2)
    FIND_LIBRARY(EGL_LIB_PATH NAMES EGL libEGL)
    FIND_PATH(GLESV2_INCLUDE_PATH GLES2/gl2.h)
    FIND_PATH(EGL_INCLUDE_PATH EGL/egl.h)
ELSE(GTA_USE_OPENGL_ES)
    FIND_PACKAGE(OpenGL)
    FIND_LIBRARY(GLEW_LIB_PATH NAMES GLEW libGLEW glew glew32 libglew libglew32)
    FIND_PATH(GLEW_INCLUDE_PATH GL/glew.h)
ENDIF(GTA_USE_OPENGL_ES)

FIND_PACKAGE(Qt4 4.6.0 COMPONENTS QtCore QtGui QtXml QtOpenGL qtmain REQUIRED)

IF(WIN32)
    STRING(REGEX MATCH "-lGL" QTOPENGL_DEPS_UNIX "${QT_QTOPENGL_LIB_DEPENDENCIES}")
    
    IF(QTOPENGL_DEPS_UNIX)
        SET(QT_QTOPENGL_LIB_DEPENDENCIES "")
    ENDIF(QTOPENGL_DEPS_UNIX)
ENDIF(WIN32)

INCLUDE(${QT_USE_FILE})

IF(APPLE)
    FIND_PATH(ICONV_INCLUDE_PATH iconv.h)
    FIND_LIBRARY(ICONV_LIB_PATH NAMES iconv libiconv)
ENDIF(APPLE)

SET(GTAFORMATS_CONFIG_H_FOUND "")
SET(GTA_CONFIG_H_FOUND "")

FOREACH(_LIB_PATH ${GTAFORMATS_INCLUDE_PATH})
	IF(EXISTS "${_LIB_PATH}/gtaformats/config.h")
		# This is a hack which prevents automatic variable expansion in a MATCHES check in CHECK_SYMBOL_EXISTS.
		# See the comment at http://public.kitware.com/Bug/view.php?id=8226
		SET(GTAFORMATSENABLESQUISH "GTAFORMATSENABLESQUISH")
		SET(GTAFORMATSENABLEPVRTEXLIB "GTAFORMATSENABLEPVRTEXLIB")
		
		CHECK_SYMBOL_EXISTS(GTAFORMATS_ENABLE_SQUISH "${_LIB_PATH}/gtaformats/config.h" GTAFORMATSENABLESQUISH)
		CHECK_SYMBOL_EXISTS(GTAFORMATS_ENABLE_PVRTEXLIB "${_LIB_PATH}/gtaformats/config.h" GTAFORMATSENABLEPVRTEXLIB)
		SET(GTAFORMATS_ENABLE_SQUISH ${GTAFORMATSENABLESQUISH})
		SET(GTAFORMATS_ENABLE_PVRTEXLIB ${GTAFORMATSENABLEPVRTEXLIB})
		SET(GTAFORMATS_CONFIG_H_FOUND "1")
		BREAK()
	ENDIF(EXISTS "${_LIB_PATH}/gtaformats/config.h")
ENDFOREACH(_LIB_PATH)
FOREACH(_LIB_PATH ${GTA_INCLUDE_PATH})
	IF(EXISTS "${_LIB_PATH}/gta/config.h")
		# See above
		SET(GTAUSEOPENGLES "GTAUSEOPENGLES")
		
		CHECK_SYMBOL_EXISTS(GTA_USE_OPENGL_ES "${_LIB_PATH}/gta/config.h" GTAUSEOPENGLES)
		SET(GTA_USE_OPENGL_ES ${GTAUSEOPENGLES})
		SET(GTA_CONFIG_H_FOUND "1")
		BREAK()
	ENDIF(EXISTS "${_LIB_PATH}/gta/config.h")
ENDFOREACH(_LIB_PATH)

IF(NOT GTAFORMATS_CONFIG_H_FOUND)
	MESSAGE(SEND_ERROR "config.h of libgtaformats was not found!")
ENDIF(NOT GTAFORMATS_CONFIG_H_FOUND)
IF(NOT GTA_CONFIG_H_FOUND)
	MESSAGE(SEND_ERROR "config.h of libgta was not found!")
ENDIF(NOT GTA_CONFIG_H_FOUND)

SET(LIBRARIES "")
SET(INCLUDES "")

SET(LIBRARIES ${LIBRARIES} "${GTA_LIB_PATH}")
SET(INCLUDES ${INCLUDES} ${GTA_INCLUDE_PATH})

SET(LIBRARIES ${LIBRARIES} "${GTAFORMATS_LIB_PATH}")
SET(INCLUDES ${INCLUDES} ${GTAFORMATS_INCLUDE_PATH})

IF(GTAFORMATS_ENABLE_SQUISH)
    SET(LIBRARIES ${LIBRARIES} "${SQUISH_LIB_PATH}")
    SET(INCLUDES ${INCLUDES} "${SQUISH_INCLUDE_PATH}")
ENDIF(GTAFORMATS_ENABLE_SQUISH)

IF(GTAFORMATS_ENABLE_PVRTEXLIB)
    SET(LIBRARIES ${LIBRARIES} "${PVRTEXLIB_LIB_PATH}")
    SET(INCLUDES ${INCLUDES} "${PVRTEXLIB_INCLUDE_PATH}")
ENDIF(GTAFORMATS_ENABLE_PVRTEXLIB)

IF(GTA_USE_OPENGL_ES)
    SET(LIBRARIES ${LIBRARIES} "${GLESV2_LIB_PATH}" "${EGL_LIB_PATH}")
    SET(INCLUDES ${INCLUDES} "${GLESV2_INCLUDE_PATH}" "${EGL_INCLUDE_PATH}")
ELSE(GTA_USE_OPENGL_ES)
    SET(LIBRARIES ${LIBRARIES} "${OPENGL_gl_LIBRARY}" "${GLEW_LIB_PATH}")
    SET(INCLUDES ${INCLUDES} "${OPENGL_INCLUDE_DIR}" "${GLEW_INCLUDE_PATH}")
ENDIF(GTA_USE_OPENGL_ES)

SET(INCLUDES ${INCLUDES} "${Boost_INCLUDE_DIRS}")

SET(LIBRARIES ${LIBRARIES} ${BULLET_LIBRARIES})
SET(INCLUDES ${INCLUDES} ${BULLET_INCLUDE_DIRS})

SET(LIBRARIES ${LIBRARIES} "${OPENGL_gl_LIBRARY}")
SET(INCLUDES ${INCLUDES} "${OPENGL_INCLUDE_DIR}")

IF(APPLE)
    SET(LIBRARIES ${LIBRARIES} "${ICONV_LIB_PATH}")
    SET(INCLUDES ${INCLUDES} "${ICONV_INCLUDE_PATH}")
ENDIF(APPLE)

SET(INCLUDES ${INCLUDES} "${CMAKE_CURRENT_BINARY_DIR}" "${CMAKE_CURRENT_BINARY_DIR}/src")
SET(INCLUDES ${INCLUDES} ${INCS})

IF(UNIX)
    SET(LIBRARIES ${LIBRARIES} "pthread")
ENDIF(UNIX)

SET(LIBRARIES ${LIBRARIES} ${QT_LIBRARIES})

SET_SOURCE_FILES_PROPERTIES(gtatools-gui_en.ts gtatools-gui_de.ts PROPERTIES
    OUTPUT_LOCATION "${CMAKE_SOURCE_DIR}/gtatools-gui/src")

QT4_WRAP_CPP(MOC_COMPILED_SRCS ${MOC_SRCS})
QT4_WRAP_UI(UIC_COMPILED_SRCS ${UIC_SRCS})
QT4_ADD_TRANSLATION(QM_FILES gtatools-gui_en.ts gtatools-gui_de.ts)

QT4_ADD_RESOURCES(QT_RESOURCE_FILES "${CMAKE_SOURCE_DIR}/gtatools-gui/src/resources.qrc")

SET(EXECUTABLE_PROPERTIES "")
IF(WIN32)
    SET(EXECUTABLE_PROPERTIES "WIN32")
ELSEIF(APPLE)
    SET(EXECUTABLE_PROPERTIES "MACOSX_BUNDLE")
ENDIF(WIN32)

INCLUDE_DIRECTORIES(${INCLUDES})

ADD_EXECUTABLE(gtatools-gui ${EXECUTABLE_PROPERTIES} ${SRCS} ${MOC_COMPILED_SRCS} ${UIC_COMPILED_SRCS} ${QM_FILES}
        ${QT_RESOURCE_FILES})
TARGET_LINK_LIBRARIES(gtatools-gui ${LIBRARIES})

INSTALL(TARGETS gtatools-gui DESTINATION bin)

